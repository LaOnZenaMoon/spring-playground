plugins {
    id 'org.asciidoctor.convert' version '2.4.0'
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation project(':core')

    // Spring framework
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Runtime H2
    implementation 'com.h2database:h2'
    implementation 'org.springframework.boot:spring-boot-devtools'

    // BulkInsert Test data
    testImplementation 'com.github.javafaker:javafaker:0.12'

    // Spring REST Docs
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

// asciidoctor -- start
asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into "BOOT-INF/classes/static/docs"
    }
}

asciidoctor.doFirst {
    println "===== start asciidoctor"
    delete file('src/main/resources/static/docs')
}

asciidoctor.doLast {
    println "===== finish asciidoctor"
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/asciidoc/html5")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir
}
// asciidoctor -- end
